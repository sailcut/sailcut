dnl Initialise autoconf
AC_INIT(sailcut, 1.3.2pre2)
AC_CONFIG_SRCDIR(acinclude.m4)
AC_CONFIG_AUX_DIR(admin)

dnl Checking host/target/build systems, for make, install etc.
AC_CANONICAL_SYSTEM 
dnl Perform program name transformation
AC_ARG_PROGRAM

dnl Initialise automake
AM_INIT_AUTOMAKE

dnl Set default prefix
AC_PREFIX_DEFAULT(/usr/local)
if test "x$prefix" = "xNONE"; then
  prefix=$ac_default_prefix
  ac_configure_args="$ac_configure_args --prefix $prefix"
fi

dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done

dnl Checks for programs
AC_PROG_CXX
AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)
AC_PROG_LIBTOOL
AC_CHECK_PROG(FIG2DEV, fig2dev, fig2dev)
AC_CHECK_PROG(PNGTOPNM, pngtopnm, pngtopnm)
AC_CHECK_PROG(PPMTOXPM, ppmtoxpm, ppmtoxpm)
AC_CHECK_PROG(ICOTOOL, icotool, icotool)
AC_CHECK_PROG(INKSCAPE, inkscape, inkscape)
AC_CHECK_PROG(XSLTPROC, xsltproc, xsltproc)
AC_CHECK_PROG(DBLATEX, dblatex, dblatex)

dnl Make compiler verbose
CXXFLAGS="$CXXFLAGS -W -Wall" 

dnl Check Qt stuff
JWB_CHECK_QT(,AC_MSG_ERROR([Qt was not found!]))
JWB_CHECK_QT_MODULE(QtSvg,,AC_MSG_ERROR([QtSvg was not found!]))
JWB_CHECK_QT_MODULE(QtXml,,AC_MSG_ERROR([QtXml was not found!]))

dnl Check OpenGL support
JWB_HAVE_QTOPENGL

dnl Check data and documentation directories
JWB_CHECK_DATADIR(sailcut)
JWB_CHECK_DOCDIR(sailcut)
JWB_CHECK_ICONDIR

dnl Platform specific switches
case "$host" in 
  *-apple-darwin* )
    platform="macos"
    ;;
  *-mingw32* )
    dnl Disable console on Win32
    LDFLAGS="-mwindows $LDFLAGS"
    platform="win32"
    ;;
esac
AM_CONDITIONAL(MACOS, test x$platform = xmacos)
AM_CONDITIONAL(WIN32, test x$platform = xwin32)

dnl Produce output
AC_CONFIG_FILES([ Makefile ])
AC_CONFIG_FILES([ sailcut.nsi ])
AC_CONFIG_FILES([ sailcut.plist ])
AC_CONFIG_FILES([ doc/Makefile ])
AC_CONFIG_FILES([ doc/en/Makefile ])
AC_CONFIG_FILES([ doc/es/Makefile ])
AC_CONFIG_FILES([ doc/fr/Makefile ])
AC_CONFIG_FILES([ icons/Makefile ])
AC_CONFIG_FILES([ src/Makefile ])
AC_CONFIG_FILES([ src/geocpp/Makefile ])
AC_CONFIG_FILES([ src/sailcpp/Makefile ])
AC_CONFIG_FILES([ src/ts/Makefile ])
AC_OUTPUT
